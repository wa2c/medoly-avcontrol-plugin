apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlinx-serialization'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
//apply plugin: 'io.fabric'
//apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 29
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.wa2c.android.medoly.plugin.action.avcontrol"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            versionNameSuffix 'd'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    // output file
    applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            def newName = "MedolyAVControlPlugin_${defaultConfig.versionName}"
            variant.outputs.all {
                // filename
                outputFileName = "${newName}.apk"
                // mapping
                variant.assembleProvider.get().doLast {
                    if ( variant.mappingFile != null && variant.mappingFile.exists() ) {
                        copy {
                            from variant.mappingFile
                            into outputFile.parent
                            rename { String fileName ->
                                "${newName}_mapping.txt"
                            }
                        }
                    }
                }
            }
        }
    }
}

repositories {
    google()
    jcenter()
    maven { url 'https://github.com/wa2c/medoly-library/raw/master/repository/' }
    maven { url 'https://github.com/wa2c/prefs/raw/master/repository/' }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Medoly library
    implementation 'com.wa2c.android:medolylibrary:1.2.7'
    // Prefs
    implementation 'com.wa2c.android:prefs:0.1.2'

    // Android
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "androidx.preference:preference:1.1.1"
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    // Networking
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.squareup.retrofit2:retrofit:2.8.1"
    implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.5.0"

    // Koin
    implementation "org.koin:koin-core:$koin_version"
    implementation "org.koin:koin-core-ext:$koin_version"
    implementation "org.koin:koin-android:$koin_version"

    // Serializer (JSON/XML)
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.20.0"
    implementation "com.tickaroo.tikxml:annotation:$tikxml_version"
    implementation "com.tickaroo.tikxml:core:$tikxml_version"
    implementation "com.tickaroo.tikxml:retrofit-converter:$tikxml_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt "com.tickaroo.tikxml:processor:$tikxml_version"

//    // Firebase
//    implementation 'com.google.firebase:firebase-core:17.4.0'
//    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    // SSDP Client
    implementation 'io.resourcepool:ssdp-client:2.4.0'
    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'
    // Dynamic Toasts
    implementation 'com.pranavpandey.android:dynamic-toasts:3.2.0'
    // App Killer Manager
    implementation 'com.thelittlefireman:AppKillerManager:2.1.1'
    // OSS Licenses Gradle Plugin
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'

    // Test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
